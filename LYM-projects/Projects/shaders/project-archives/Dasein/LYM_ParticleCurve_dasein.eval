/*
* This program is free software; you can redistribute it and/or
* modify it under the terms of the GNU Lesser General Public License
* as published by the Free Software Foundation; either version 2.1
* of the License, or (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
* Lesser General Public License for more details.
*
* You should have received a copy of the GNU Lesser General Public
* License along with this program; if not, write to the Free
* Software Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
* 02111-1307, USA.
*/
/*
based on Francesco Caruso GLSLParametricCurves
https://github.com/fcaruso/GLSLParametricCurve
*/

#version 420

layout (isolines, equal_spacing, ccw) in;

uniform mat4 vp_modelMatrix, vp_viewMatrix, vp_projMatrix;

/////////////////////////////////////
// INPUT VARYINGS
in VertexData {
    float radius;
    vec3 color;
    vec2 speed;
    vec2 replayTrackLocation;
} VertexIn[];

// OUTPUT VARYINGS
out VertexData {
    vec2 texCoord;
    vec2 normal;
    float radius;
    vec3 color;
    vec2 speed;
    vec2 replayTrackLocation;
} VertexOut;

///////////////////////////////////////////////////
// function to evaluate  the Bezier curve
vec2 bezier(float u, vec2 p0, vec2 p1, vec2 p2, vec2 p3)
{
	float B0 = (1.-u)*(1.-u)*(1.-u);
	float B1 = 3.*u*(1.-u)*(1.-u);
	float B2 = 3.*u*u*(1.-u);
	float B3 = u*u*u;

	vec2 p = B0*p0 + B1*p1 + B2*p2 + B3*p3;
	return p;
} 
vec2 bezierTangent(float u, vec2 p0, vec2 p1, vec2 p2, vec2 p3)
{
	float square1 = 3.*(1.-u)*(1.-u);
	float square = 3.*u*u;
	float diag = 6.*(1.-u)*u;;
	float B0 = -square1;
	float B1 = square1 - diag;
	float B2 = diag - square;
	float B3 = square;

	vec2 p = B0*p0 + B1*p1 + B2*p2 + B3*p3;
	return p;
} 

void main()
{
	// gl_TessCoord.x represents the distance along one of the lines
    float u = gl_TessCoord.x;
    // gl_TessCoord.y specifies which line the vertex is for
    float v = gl_TessCoord.y;

    // the patch is made of four
    // control points that can be accessed
    // through gl_in[0-3]

	vec2 p0 = vec2( gl_in[0].gl_Position );
	vec2 p1 = vec2( gl_in[1].gl_Position );
	vec2 p2 = vec2( gl_in[2].gl_Position );
	vec2 p3 = vec2( gl_in[3].gl_Position );
	vec4 pos = vec4( bezier( u, p0, p1, p2, p3), 0.0, 1.0);
	// in case of several diverging isolines + 8 * u * vec2(0,v,0), 1. );

	// position
	gl_Position = vp_projMatrix * vp_viewMatrix *  vp_modelMatrix * pos;

	// normal
	vec2 tang = bezierTangent( u, p0, p1, p2, p3);
	VertexOut.normal = normalize(vec2(tang.y,-tang.x))/50;

	// passed parameters
	VertexOut.texCoord = vec2(u,0);

	// passing input variables to geometry shader
	VertexOut.color = VertexIn[0].color;
	VertexOut.radius = VertexIn[0].radius;
	VertexOut.speed = VertexIn[0].speed;
	VertexOut.replayTrackLocation = VertexIn[0].replayTrackLocation;
}
